
cu = read_ascii('../xafsdata/cu.chi')

fitparams = group(amp  = param(1, vary=True),
                  de0  = param(1, vary=True),
                  ss2  = param(0.002, vary=True),
                  delr = param(0, vary=True))

path1 = feffpath('feffcu01.dat',  s02='amp', sigma2='ss2',  deltar='delr')


# can also set a path parameter this way:
path1.e0 = 'de0'

trans = feffit_transform(kmin=3, kmax=17, kw=2, dk=3, rmin=1.4, rmax=3.0)
dset  = feffit_dataset(data=cu, pathlist=[path1], transform=trans)

out = feffit(fitparams, dset)
print( feffit_report(out))

ox, oy, map = chi2_map(out, 'de0', 'delr')
imshow(map, y=ox, x=oy)
